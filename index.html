
<script src="script.js"></script>

<script id="title" type="text/plain">Ray Tracer</script>

<script id="description" type="text/plain">
Two lights, three spheres, three level deep reflection (1 original ray + 3 reflected)
</script>

<script id="shader" type="x-shader/x-fragment">

vec3 BLACK = vec3(0., 0., 0.);
vec3 RED = vec3(1.0, 0., 0.);
vec3 GREEN = vec3(0., 1.0, 0.);
vec3 BLUE = vec3(0., 0., 1.0);

const float HALF_FOG_AT = 3.0;
const float ALL_FOG_AT = 9.0 * HALF_FOG_AT;
vec3 fogColor = vec3(0.3, 0.3, 0.35);

float INFINITE_DISTANCE = ALL_FOG_AT;

const int NUM_SPHERES = 2;
uniform mat4 trsSpheres[NUM_SPHERES];
uniform mat4 trsInvSpheres[NUM_SPHERES];
uniform vec3 colSpheres[NUM_SPHERES];
uniform vec3 colPropSpheres[NUM_SPHERES];

const int NUM_PLANES = 1;
uniform mat4 trsPlanes[NUM_PLANES];
uniform mat4 trsInvPlanes[NUM_PLANES];
uniform vec3 colPlanes[NUM_PLANES];
uniform vec3 colPropPlanes[NUM_PLANES];

const int NUM_CUBES = 1;
uniform mat4 trsCubes[NUM_CUBES];
uniform mat4 trsInvCubes[NUM_CUBES];
uniform vec3 colCubes[NUM_CUBES];
uniform vec3 colPropCubes[NUM_CUBES];

uniform vec4 UNIT_CUBE_FACES[6];

const int NUM_INFINITE_LIGHTS = 2;
uniform vec4 infiniteLights[NUM_INFINITE_LIGHTS];
uniform float AMBIENT_REFLECTANCE;

float fl = 1.0;  // focal length
float ar = 1.0;  // aspect ratio: width / height

vec4 eye_l = vec4(0., 0., fl, 1.);
vec4 lookAt_l = vec4(0., 0., 0., 1.);
vec4 eye_d = normalize(lookAt_l - eye_l);

float surface_t, surface_t_backup;   // distance t
vec3  surface_l, surface_l_backup;   // loc
vec3  surface_n, surface_n_backup;   // normal
vec3  surface_c, surface_c_backup;   // color
vec3  surface_p, surface_p_backup;   // color properties (shiny, metallic, tranparent)

void stashSurface() {
    surface_t_backup = surface_t;
    surface_l_backup = surface_l;
    surface_n_backup = surface_n;
    surface_c_backup = surface_c;
    surface_p_backup = surface_p;
}

void popSurfaceStash() {
    surface_t = surface_t_backup;
    surface_l = surface_l_backup;
    surface_n = surface_n_backup;
    surface_c = surface_c_backup;
    surface_p = surface_p_backup;
}

vec4 fixIntersectionPoint(vec4 loc, vec4 dir) {
    return loc + 0.0005 * dir;
}
/*

vec3 computeDirectedLights( vec3 surfaceLoc,
                            vec3 surfaceNormal,
                            vec3 reflectedDir,
                            vec3 surfaceCol,
                            vec3 surfaceColProperties)
{
    float shiny    = surfaceColProperties.x;
    float metallic = surfaceColProperties.y;

    vec3 directedColor = vec3(0., 0., 0.);
    
    for (int idx = 0; idx < NUM_INFINITE_LIGHTS; ++idx) {
        
        vec3 lightDir = normalize(infiniteLights[idx].xyz);
        vec3 cDiffused = vec3(0., 0., 0.);
        
        vec3 lDiffuse = vec3(0.7, 0.7, 0.7);   //TODO: color of the light
        vec3 lSpecular = metallic * surfaceCol + (1. - metallic) * lDiffuse;

        if( dot(lightDir, surfaceNormal) > 0.0 ) {
            vec4 s = vec4(fixIntersectionPoint(surfaceLoc, lightDir), 1.0);
            if( intersectAllWithBackup(s, vec4(lightDir, 0.0)) != INFINITE_DISTANCE ) { continue; }

            // diffused light
            cDiffused = lDiffuse * dot( surfaceNormal, lightDir );
        }

        // specular light
        vec3 cSpecular = lSpecular * pow(max(0., dot( lightDir, reflectedDir )), shiny);
        
        //                    intensity        *       sum_of_colors
        directedColor += infiniteLights[idx].w * (cDiffused + cSpecular);
    };
    
    return directedColor;
}
vec3 computeColor( vec3 surfaceLoc,
                   vec3 surfaceNormal,
                   vec3 reflectedDir,
                   vec3 surfaceCol,
                   vec3 surfaceColProperties)
{
    vec3 ambientColor  = surfaceCol.xyz;
    vec3 diffusedColor = computeDirectedLights(surfaceLoc, surfaceNormal, reflectedDir, surfaceCol, surfaceColProperties);

    return
    AMBIENT_REFLECTANCE         * ambientColor +
    (1.0 - AMBIENT_REFLECTANCE) * diffusedColor;
}
*/


// returns color and saves intersection info in surface_... variables
/*
vec3 simulateRay(vec4 loc, vec4 dir) {
    vec3 color = vec3(0., 0., 0.);

    if( intersectAll(loc, dir) != INFINITE_DISTANCE ) {
        return vec3(0.7, 0.2, 0.2);

        //vec4 reflectionDir = reflect(dir, vec4(surface_n, 1.0));
        //color = computeColor( surface_l, surface_n, reflectionDir.xyz, surface_c, surface_p );
    }

    return color;
}
*/

vec4 res_intersectSphere_point;
vec4 res_intersectSphere_normal;

void intersectAllSpheres(vec4 loc, vec4 dir) {

    for (int i = 0; i < NUM_SPHERES; i++) {
        mat4 INV = trsInvSpheres[i];

        vec3 v = (INV * loc).xyz;
        vec3 w = normalize(INV * dir).xyz;

        float B = 2.0 * dot(w, v);
        float C = dot(v, v) - 1.0;
        float d = B*B - 4.0*C;

        if( d < 0.0 ) continue;                // missed it

        float t = (-B - sqrt(d)) / 2.0;        // t in sphere's coordinates
        if( t <= 0.0 ) continue;               // behind us

        vec3 p = v + t * w;                    // intersection point in sphere's coordinates
        vec3 norm = normalize(trsSpheres[i] * vec4(p, 0.)).xyz;
        p = (trsSpheres[i] * vec4(p, 1.)).xyz; // intersection point in world coordinates
        t = length(p - loc.xyz);

        if( t < surface_t ) {
            surface_t = t;
            surface_l = p;
            surface_n = norm;
            surface_c = colSpheres[i];
            surface_p = colPropSpheres[i];
        }
    }
}

void intersectAllPlanes(vec4 loc, vec4 dir) {

    for (int i = 0; i < NUM_PLANES; i++) {
        vec4 v = trsInvPlanes[i] * loc;
        vec4 w = trsInvPlanes[i] * dir;

        vec4 l = vec4(0., 1., 0., 1.);  // plane

        float t = dot(l, w);
        if( t == 0. ) continue;    // parallel to the plane
        if( t > 0. )  continue;    // plane is behind us

        t = - dot(l, v) / t;
        vec4 p = v + t * w;        // intersection point in plane's coordinates
        p = trsPlanes[i] * p;      // intersection point in world coordinates
        t = length(p - loc);       // t in world coordinates

        if( t < surface_t ) {
            surface_t = t;
            surface_l = p.xyz;
            surface_n = (trsPlanes[i] * vec4(0., 1., 0., 0.)).xyz;
            surface_c = colPlanes[i];
            surface_p = colPropPlanes[i];
        }
    };
}

void intersectAllCubes(vec4 loc, vec4 dir) {

    for (int i = 0; i < NUM_CUBES; i++) {

        vec4 v = trsInvCubes[i] * loc;
        vec4 w = trsInvCubes[i] * dir;

        float maxEnterT = -INFINITE_DISTANCE;
        float minExitT = INFINITE_DISTANCE;
        bool outside = false;

        vec4 enterPlane;
        vec4 enterPlaneNormal;

        for (int i = 0; i < 6; i++) {   // for each face...
            if( outside ) break;
            float lw = dot(UNIT_CUBE_FACES[i], w);
            float lv = dot(UNIT_CUBE_FACES[i], v);
            
            if( lw == 0. ) {
                if( lv > 0. ) { outside = true; continue; }  // ray is outside the half-space
                else continue;                               // ray is inside but parallel to half-space
            }

            if( lw < 0. ) {
                float t = - lv / lw;
                if( t > maxEnterT ) {
                    maxEnterT = t;
                    enterPlane = UNIT_CUBE_FACES[i];
                    enterPlaneNormal = vec4(UNIT_CUBE_FACES[i].xyz, 0.);
                }
            }
            if( lw > 0. ) {
                float t = - lv / lw;
                if( t < minExitT ) {
                    minExitT = t;
                }
            }
        }

        if( maxEnterT > minExitT )  continue;  // missed it
        if( maxEnterT < 0. )  continue;  // behind us

        vec4 p = v + maxEnterT * w;    // intersection in cube's coordinates
        p = trsCubes[i] * p;           // intersection in world coordinates
        float t = length(loc - p);
        if( t < surface_t ) {
            surface_t = t;
            surface_l = p.xyz;
            surface_n = (trsCubes[i] * enterPlaneNormal).xyz;
            surface_c = colCubes[i];
            surface_p = colPropCubes[i];
        }
    }
}

float intersectAll(vec4 loc, vec4 dir) {
    surface_t = INFINITE_DISTANCE;

    // each of the intersectAll* functions is responsible for setting:
    // - surface_t
    // - surface_l
    // - surface_n
    // - surface_c
    // - surface_p
    intersectAllSpheres(loc, dir);
    intersectAllPlanes(loc, dir);
    intersectAllCubes(loc, dir);

    return surface_t;
}

float intersectAllWithBackup(vec4 loc, vec4 dir) {
    stashSurface();
    float ret = intersectAll(loc, dir);
    popSurfaceStash();
    return ret;
}

vec3 computeDirectedLight(vec3 reflectedDir) {
    float shiny    = surface_p.x;
    float metallic = surface_p.y;

    vec3 directedColor = BLACK;

    for (int idx = 0; idx < NUM_INFINITE_LIGHTS; ++idx) {
        
        vec3 lightDir = normalize(infiniteLights[idx].xyz);
        vec3 cDiffused = BLACK;
        
        vec3 lDiffuse = vec3(0.7, 0.7, 0.7);   //TODO: color of the light
        vec3 lSpecular = metallic * surface_c + (1. - metallic) * lDiffuse;

        if( dot(lightDir, surface_n) > 0. ) {
            vec4 s = fixIntersectionPoint(vec4(surface_l, 1.), vec4(lightDir, 0.));
            if( intersectAllWithBackup(s, vec4(lightDir, 0.0)) != INFINITE_DISTANCE ) { continue; }

            // diffused light
            cDiffused = lDiffuse * dot( surface_n, lightDir );
        }

        // specular light
        vec3 cSpecular = lSpecular * pow(max(0., dot( lightDir, reflectedDir )), shiny);

        //                    intensity        *       sum_of_colors
        directedColor += infiniteLights[idx].w * (cDiffused + cSpecular);
    }

    return directedColor;
}

vec3 computeColor(vec3 reflectedDir) {
    if( surface_t >= ALL_FOG_AT )
        return fogColor;

    vec3 ambientColor  = surface_c;
    vec3 directedColor = computeDirectedLight(reflectedDir);

    vec3 color =
    AMBIENT_REFLECTANCE         * ambientColor +
    (1.0 - AMBIENT_REFLECTANCE) * directedColor;

    // seen through fog
    float stf = pow(0.5, surface_t / HALF_FOG_AT);
    color = stf * color + (1.0 - stf) * fogColor;

    return color;
}

vec3 simulateRay(vec4 loc, vec4 dir) {
    
    intersectAll(loc, dir);

    return computeColor(reflect(dir.xyz, surface_n));
}

void main(void) {

    float screenX = vUV.x;
    float screenY = vUV.y;
    float time = uTime;
    vec3 color = vec3(0., 0., 0.);
    vec4 rayDir = normalize(vec4((screenX - 0.5) * ar, screenY - 0.5, -fl, 0.0));

    float reflectionScaleFactor = 1.0;

    color = simulateRay(eye_l, rayDir);

    // reflection ray depth 1
    if( surface_t != INFINITE_DISTANCE ) {
        reflectionScaleFactor *= surface_p.y;
        rayDir = reflect(rayDir, vec4(surface_n, 0.));
        color += reflectionScaleFactor * simulateRay(fixIntersectionPoint(vec4(surface_l, 1.0), rayDir), rayDir);
    }

    gl_FragColor = vec4(color, 1.0);
}
</script>

<script>start()</script>
