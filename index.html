
<script src="script.js"></script>

<script id="title" type="text/plain">Ray Tracer</script>

<script id="description" type="text/plain">
Two lights, two spheres, blah-blah-blah...
</script>

<script id="shader" type="x-shader/x-fragment">

float INFINITE_DISTANCE = 10000000000000000000000000000000000000.;   // TODO: probably a better way to do this because programmers are smart and include upper limit values in their libraries somehow?

const int NUM_SPHERES = 3;
const int NUM_INFINITE_LIGHTS = 2;



uniform vec4 infiniteLights[NUM_INFINITE_LIGHTS];
//vec3 infiniteLight_d = normalize(vec3(-1., 1., -0.2));

uniform vec4 sphereLocs[NUM_SPHERES];
uniform vec3 sphereCols[NUM_SPHERES];

uniform float AMBIENT_REFLECTANCE;

float fl = 1.0;  // focal length
float ar = 1.0;  // aspect ratio: width / height

vec4 eye_l = vec4(0., 0., fl, 1.);
vec4 lookAt_l = vec4(0., 0., 0., 1.);
vec4 eye_d = normalize(lookAt_l - eye_l);

vec4 scanDir(float screenX, float screenY) {
    return normalize(vec4((screenX - 0.5) * ar, screenY - 0.5, -fl, 0.0));
}

// Returns t (for p = v + tw) if intersection exists, otherwise returns INFINITE_DISTANCE.
// Pre-condition: dir is normalized.
float intersectSphere(vec3 loc, vec3 dir, vec3 center, float radius) {
    // dir must be normalized, so A = dir * dir will always be 1.
    float B = 2.0 * dot(dir, (loc - center));
    float C = dot(loc-center, loc-center) - radius * radius;

    float d = B*B - 4.0*C;
    if (d < 0.0) return INFINITE_DISTANCE; else return (-B - sqrt(d)) / 2.0;
}

float surface_t, surface1_t;
vec3  surface_l, surface1_l;
vec3  surface_n, surface1_n;
vec3  surface_c, surface1_c;

void saveSurface1() {
    surface1_t = surface_t;
    surface1_l = surface_l;
    surface1_n = surface_n;
    surface1_c = surface_c;
}

// sets surface_t, surface_l, surface_n, surface_c
float intersectAll(vec3 loc, vec3 dir) {
    surface_t = INFINITE_DISTANCE;

    // add objects here
    for( int i = 0; i < NUM_SPHERES; ++i ) {
        vec3  c = sphereLocs[i].xyz;
        float r = sphereLocs[i].w;

        float tPrime = intersectSphere(loc, dir, c, r);
        if( tPrime < surface_t && tPrime > 0.) {
            surface_t = tPrime;
            surface_l = loc + surface_t * dir;
            surface_n = normalize(surface_l - c);
            surface_c = sphereCols[i];
        }
    }

    return surface_t;
}

vec3 reflectionDir(vec3 incoming, vec3 normal) {
    return 2. * dot(-incoming, normal) * normal + incoming;
}

vec3 fixIntersectionPoint(vec3 loc, vec3 dir) {
    return loc + 0.0001 * dir;
}

vec3 computeDiffusedColor(vec3 surfaceLoc, vec3 surfaceNormal, vec3 surfaceCol) {
    float diffusedColorIntensity = 0.;
    
    for (int idx = 0; idx < NUM_INFINITE_LIGHTS; ++idx) {
        
        vec3 lightDir = normalize(infiniteLights[idx].xyz);
        if( dot(lightDir, surfaceNormal) < 0.0 ) { continue; }

        vec3 s = fixIntersectionPoint(surfaceLoc, lightDir);
        if( intersectAll(s, lightDir) == INFINITE_DISTANCE ) {
            float lightIntensity = infiniteLights[idx].w;
            diffusedColorIntensity += lightIntensity * dot( surfaceNormal, lightDir );
        }
    };
    
    return surfaceCol * diffusedColorIntensity;
}

vec3 computeColor(vec3 surfaceLoc, vec3 surfaceNormal, vec3 surfaceCol) {
    vec3 ambientColor  = surfaceCol;
    vec3 diffusedColor = computeDiffusedColor(surfaceLoc, surfaceNormal, surfaceCol);

    return
    AMBIENT_REFLECTANCE         * ambientColor +
    (1.0 - AMBIENT_REFLECTANCE) * diffusedColor;
}

void main(void) {

    float screenX = vUV.x;
    float screenY = vUV.y;
    float time = uTime;
    vec3 color = vec3(0., 0., 0.);
    vec4 scan_d = scanDir(screenX, screenY);

    if( intersectAll(eye_l.xyz, scan_d.xyz) != INFINITE_DISTANCE ) {
        saveSurface1();
        color = computeColor(surface1_l, surface1_n, surface1_c);
    }
    else { color = vec3(0., 0., screenY); }

    if( uMousePressed == 1.0 ) color = vec3(1., 1., 1.) - color;
    gl_FragColor = vec4(color, 1.0);
}
</script>

<script>start()</script>
